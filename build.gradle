plugins {
    id 'java'
    id 'eclipse'
    id 'com.google.protobuf' version '0.9.4'
}

eclipse.project.name = 'MyProject'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

def grpcVersion = '1.59.1'
def protobufVersion = '3.25.1'
def protocVersion = protobufVersion

sourceSets {
    main {
        proto {
            srcDirs = ['protobuf']
        }
        java {
            srcDirs = [
                'src',
                'build/generated/source/proto/main/grpc',
                'build/generated/source/proto/main/java'
            ]
        }
    }

    test {
        java {
            srcDirs = ['test']
        }
    }
}

repositories {
    mavenCentral()
}
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protocVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'org.mockito:mockito-core:5.10.0'
    implementation 'com.google.code.gson:gson:2.13.1'
    implementation 'fr.inria.gforge.spoon:spoon-core:11.2.0'

    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-services:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly "org.apache.tomcat:annotations-api:6.0.53"
    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation "io.grpc:grpc-inprocess:${grpcVersion}"
}

test {
    useJUnitPlatform()
}
